options {
    LOOKAHEAD = 2;
}
PARSER_BEGIN(Evaluator)

import java.util.LinkedHashMap;
import java.util.regex.Pattern;
import java.util.regex.Matcher;

public class Evaluator {

    public static LinkedHashMap LookupTable = new LinkedHashMap();

    public static void main(String[] args) {
        try {
            populateTable(args[0]);
            System.out.println(new Evaluator(new java.io.StringReader(args[0])).S());
        } catch (Throwable e) {
            // Catching Throwable is ugly but JavaCC throws Error objects!
            System.out.println("Failed: " + e.getMessage());
        }
    }

    public static void populateTable(String exp){
    // [A-Z] is a character class for pattern which specifies A through Z

    Pattern pattern = Pattern.compile("[A-E[G-S[U-Z]]]");
    Matcher matcher = pattern.matcher(exp);

      while(matcher.find()){

        String var = exp.substring(matcher.start(), matcher.start()+1);
        boolean boolv = true;

        if(LookupTable.get(var)==null){
            LookupTable.put(var, boolv);
        }
      }
    }

    // Given a set of bindings, return the next set of bindings
    // Ex: TFF returns TFT
    public String successor(String bindings){
        bindings = bindings.replace('T', '1');
        bindings = bindings.replace('F', '0');
        int value = Integer.parseInt(bindings, 2);
        value++;

        if(value >= 8){
            // Cannot return past TTT
            return "";
        }
        else{
            String newbindings = Integer.toString(value);
            newbindings = newbindings.replace('1', 'T');
            newbindings = newbindings.replace('0', 'F');
            return newbindings;
        }
    }
}

PARSER_END(Evaluator)

SKIP:  { " " | "\t" | "\n" | "\r" } // Skip whitespace
TOKEN: { "(" | ")" | "&&" | "||" | "->" | "<-" | "<->" | "!" | <VAR : (["A"-"E", "G"-"S", "U"-"Z"])> | <BOOL: (["T", "F"])> }

boolean S():{ boolean result; } {
    result = BIJ() <EOF> {
        return result;
    }
}

boolean BIJ(): { boolean result, rhs; } {
    result = IMP() ("<->" rhs = BIJ() { result = result && rhs || !result && !rhs; } )* {
        return result;
    }
}

boolean IMP(): { boolean result, rhs; } {
    result = OR() ("->"  rhs = IMP() { result = !result || rhs; } )* {
        return result;
    }
}

boolean OR(): { boolean result, rhs; } {
    result = AND() ("||"  rhs = OR() { result = result || rhs; } )* {
        return result;
    }
}

boolean AND(): { boolean result, rhs; } {
    result = NOT() ("&&" rhs = AND() { result = result && rhs; } )* {
        return result;
    }
}

boolean NOT(): { boolean result; } {
    "!" result = BOOL() {
        return !result;
    }
    | result = BOOL() {
        return result;
    }
}

boolean BOOL(): { Token n; boolean result; } {
    n = <VAR> {
        return (boolean)Evaluator.LookupTable.get(n.image);
    }
    | n = <BOOL> {
        if(n.image.equals("T"))
            return true;
        return false;
    }
    | "(" result = BIJ() ")" {
        return result;
    }
}

